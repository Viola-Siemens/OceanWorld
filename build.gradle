buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net' }
        mavenCentral()
        maven { name="sponge"; url 'https://repo.spongepowered.org/repository/maven-public/' }
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '[6.0,6.2)', changing: true
        classpath 'org.spongepowered:mixingradle:0.7-SNAPSHOT'
    }
}

plugins {
    id 'eclipse'
    id 'idea'
    id 'maven-publish'
}
apply plugin: 'net.minecraftforge.gradle'

base {
    archivesName = 'oceanworld'
}

version = '7.0.0'
group = 'com.hexagram2021.oceanworld'
archivesBaseName = 'oceanworld'

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

println "Java: ${System.getProperty 'java.version'}, JVM: ${System.getProperty 'java.vm.version'} (${System.getProperty 'java.vendor'}), Arch: ${System.getProperty 'os.arch'}"
minecraft {
    mappings channel: 'official', version: '1.20'
    
    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    enableIdeaPrepareRuns = true
    copyIdeResources = true
    generateRunFolders = true

    runs {
        client {
            workingDirectory project.file('run/client')
            
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            property 'forge.enabledGameTestNamespaces', 'oceanworld'

            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile',"${buildDir}/createSrgToMcp/output.srg"
            
            mods {
                oceanworld {
                    source sourceSets.main
                }
            }
        }
        
        server {
            workingDirectory project.file('run/server')
            
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            property 'forge.enabledGameTestNamespaces', 'oceanworld'

            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile',"${buildDir}/createSrgToMcp/output.srg"
            
            mods {
                oceanworld {
                    source sourceSets.main
                }
            }
        }
        
        gameTestServer {
            workingDirectory project.file('run/test_server')
            
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            property 'forge.enabledGameTestNamespaces', 'oceanworld'

            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile',"${buildDir}/createSrgToMcp/output.srg"
            
            mods {
                oceanworld {
                    source sourceSets.main
                }
            }
        }
        
        data {
            workingDirectory project.file('run/data')
            
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'

            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile',"${buildDir}/createSrgToMcp/output.srg"

            args '--mod', 'oceanworld', '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')
            
            mods {
                oceanworld {
                    source sourceSets.main
                }
            }
        }
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
    maven {
        name 'Mod Maven'
        url 'https://modmaven.dev/'
    }
	maven {
        name 'theillusivec4\'s Maven'
        url 'https://maven.theillusivec4.top/'
    }
    maven {
        name 'Curse Maven'
        url 'https://cursemaven.com'
        content {
            includeGroup "curse.maven"
        }
    }
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.20-46.0.14'
	
	compileOnly fg.deobf("top.theillusivec4.curios:curios-forge:5.4.7+1.20.1")
    runtimeOnly fg.deobf("mezz.jei:jei-1.20-forge:14.0.0.11")
    runtimeOnly fg.deobf("curse.maven:jade-324717:4986594")
    runtimeOnly fg.deobf("curse.maven:custom_world_generation-913105:4835163")
}

jar {
    manifest {
        attributes([
                "Specification-Title"     : "Ocean World",
                "Specification-Vendor"    : "Hexagram",
                "Specification-Version"   : "1",
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : project.jar.archiveVersion,
                "Implementation-Vendor"   : "Hexagram",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
    from "LICENSE"
}

jar.finalizedBy('reobfJar')

task sourceJar(type: Jar) {
    archiveClassifier.set('sources')
    from sourceSets.main.allJava
}

artifacts {
    archives sourceJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
            artifact sourceJar
        }
    }
    repositories {
        maven {
            url "file://${project.projectDir}/mcmodsrepo"
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}
